pkgname=tree-sitter-jsdoc
pkgver=0.23.0
pkgrel=1
pkgdesc='Jsdoc grammar for tree-sitter'
arch=(x86_64)
url=https://github.com/tree-sitter/tree-sitter-jsdoc/
license=(MIT)
groups=(tree-sitter-grammars)
makedepends=(
  git
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("${url}archive/refs/tags/v${pkgver}.zip")
sha256sums=('ee0e20ad1b0df7796a72c6282c83f435320f14aa218f00608abce8a41f75f4ca')

prepare()
{
  cd $pkgname-${pkgver}
  tree-sitter generate --no-bindings src/grammar.json
}

build()
{
  cd $pkgname-${pkgver}
  make PREFIX=/usr PARSER_URL=$url
}

package()
{
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname-${pkgver}
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
