pkgname=vkd3d-proton
pkgver=2.6
pkgrel=1
pkgdesc="Direct3D 12 to Vulkan translation library (Windows DLL binary files)"
url="https://github.com/HansKristian-Work/vkd3d-proton"
license=('LGPL')
arch=('x86_64')
depends=('vulkan-icd-loader')
optdepends=('lib32-vulkan-icd-loader: 32-bit Windows application support')
provides=('vkd3d-proton')
conflicts=('vkd3d-proton-git' 'vkd3d-proton-bin')
options=(!strip)
source=("https://github.com/HansKristian-Work/${pkgname}/releases/download/v$pkgver/${pkgname}-$pkgver.tar.zst")
sha256sums=('c76d2e5ab28230c746c6a5dc112d23e3db9963b456cf390e5ac67ea2e8674aa5')

package() {
	install -D "${pkgname}-$pkgver"/x86/* -t "$pkgdir/usr/share/vkd3d-proton/x86"
	install -D "${pkgname}-$pkgver"/x64/* -t "$pkgdir/usr/share/vkd3d-proton/x64"
	install "${pkgname}-$pkgver"/setup_vkd3d_proton.sh -t "$pkgdir/usr/share/vkd3d-proton/"
	install -d "$pkgdir/usr/bin"
	ln -s /usr/share/vkd3d-proton/setup_vkd3d_proton.sh "$pkgdir/usr/bin/setup_vkd3d_proton"
}
