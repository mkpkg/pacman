pkgname=sublime-text-3
pkgver=3.3211
pkgrel=1
pkgdesc="Sophisticated text editor for code, html and prose"
arch=('x86_64')
url='https://www.sublimetext.com/3'
license=('custom')
depends=('libpng' 'gtk2')

replaces=('sublime-text-3-imfix')

conflicts=('sublime-text-4-dev'
           'sublime-text-3-imfix'
           'sublime-text-imfix'
           'sublime-text-nightly'
           'sublime-text-dev'
           'sublime-text2'
           'sublime-text')

provides=('sublime-text' 'sublime' 'subl' 'sle')

source=(
    "https://download.sublimetext.com/sublime_text_3_build_${pkgver:2}_x64.tar.bz2"
    'LICENSE'
)
sha256sums=('0b3c8ca5e6df376c3c24a4b9ac2e3b391333f73b229bc6e87d0b4a5f636d74ee'
            '339dbd1b6ae3e699809afcdd6c690299e6fa0d385aa2ef658a52d336a3f8107f')

package() {
    cd "${srcdir}"

    # Install sublime text 3
    install -dm755 "${pkgdir}/opt"
    cp --preserve=mode -r "sublime_text_3" "${pkgdir}/opt/sublime_text_3"

    # Install icons
    for res in 16x16 32x32 48x48 128x128 256x256; do
        install -dm755 "${pkgdir}/usr/share/icons/hicolor/${res}/apps"
        ln -s "/opt/sublime_text_3/Icon/${res}/sublime-text.png" "${pkgdir}/usr/share/icons/hicolor/${res}/apps/sublime-text.png"
    done

    # Install desktop entry and executable
    install -dm755 "${pkgdir}/usr/share/applications"
    install -Dm644 "sublime_text_3/sublime_text.desktop" "${pkgdir}/usr/share/applications/sublime_text.desktop"
    sed -i 's#/opt/sublime_text/sublime_text#subl#g' "${pkgdir}/usr/share/applications/sublime_text.desktop"
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/opt/sublime_text_3/sublime_text" "${pkgdir}/usr/bin/subl"
    ln -s "/opt/sublime_text_3/sublime_text" "${pkgdir}/usr/bin/sle"

    # Install license file
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
