pkgname=tree-sitter-meson
pkgver=1.2.1
pkgrel=1
pkgdesc='meson grammar for tree-sitter'
arch=(x86_64)
url=https://github.com/tree-sitter-grammars/tree-sitter-meson/
license=(MIT)
groups=(tree-sitter-grammars)
makedepends=(
  git
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("${url}archive/refs/tags/${pkgver}.zip")
sha256sums=('d10c76cb5f944647397933b6562dd22fb61e02bbb1c149881384ea64ae18634f')

prepare()
{
  cd $pkgname-${pkgver}
  tree-sitter generate --no-bindings src/grammar.json
}

build()
{
  cd $pkgname-${pkgver}
  make PREFIX=/usr PARSER_URL=$url
}

package()
{
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname-${pkgver}
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
