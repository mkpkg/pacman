pkgname=tree-sitter-ql
pkgver=0.23.0
pkgrel=1
pkgdesc='CodeQL grammar for tree-sitter'
arch=(x86_64)
url=https://github.com/tree-sitter/tree-sitter-ql/
license=(MIT)
groups=(tree-sitter-grammars)
makedepends=(
  git
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("${url}/archive/refs/tags/v${pkgver}.zip")
sha256sums=('04cab678f4e89f744d4baa44cb0510c0590c9ee898f31911767a7b1a222e8372')

prepare()
{
  cd $pkgname-${pkgver}
  tree-sitter generate --no-bindings src/grammar.json
}

build()
{
  cd $pkgname-${pkgver}
  make PREFIX=/usr PARSER_URL=$url
}

package()
{
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname-${pkgver}
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
