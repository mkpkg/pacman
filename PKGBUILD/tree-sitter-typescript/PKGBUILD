pkgname=tree-sitter-typescript
pkgver=0.21.2
pkgrel=1
pkgdesc='Typescript grammar for tree-sitter'
arch=(x86_64)
url=https://github.com/tree-sitter/tree-sitter-typescript/
license=(MIT)
groups=(tree-sitter-grammars)
makedepends=(
  git
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("${url}/archive/refs/tags/v${pkgver}.zip")
sha256sums=('f4199e47ef9e0536d6a9ebf9dfb92dd66d31a7e4b7ea9353c978c7b7fa8e745e')

prepare()
{
  cd $pkgname-${pkgver}
  tree-sitter generate --no-bindings src/grammar.json
}

build()
{
  cd $pkgname-${pkgver}
  make PREFIX=/usr PARSER_URL=$url
}

package()
{
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname-${pkgver}
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
