_pkgbase=gtkglext
pkgname=lib32-$_pkgbase
pkgver=1.2.0
pkgrel=11
pkgdesc="OpenGL extensions for GTK+ (32-bit)"
arch=('x86_64')
url="http://gtkglext.sourceforge.net/"
license=('LGPL')
depends=('lib32-gtk2' 'lib32-libxmu' 'lib32-glu' 'lib32-pangox-compat' "$_pkgbase")
makedepends=('gcc-multilib' 'lib32-mesa' 'python')
options=('!libtool')
source=(
  "https://mirrors.xie.ke/mkpkg/software/gtkglext/gtkglext-${pkgver}.tar.bz2"
  'gtk2.20.patch'
  'gdkglshapes.patch'
)
md5sums=('ed7ba24ce06a8630c07f2d0ee5f04ab4'
  'e5a87ec3f2d0e616c6f32f90c3f7237f'
  '612bae44f3cc9db93687af2b5d86a02f')

prepare() {
  cd ${_pkgbase}-${pkgver}
  patch -Np1 -i ../gtk2.20.patch
  patch -Np1 -i ../gdkglshapes.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbase}-${pkgver}
  autoconf --force
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
}
