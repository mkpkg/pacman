pkgname=android-studio-canary
pkgver=2024.1.2.5
pkgrel=1
pkgdesc="The Official Android IDE (Canary branch)"
arch=('x86_64')
url="http://tools.android.com/"
license=('APACHE')
makedepends=('unzip' 'zip')
depends=( 'alsa-lib' 'fontconfig' 'freetype2' 'gcc-libs' 'bzip2' 'libedit' 'libxcrypt-compat' 'libxml2' 'libxrender' 'libxtst' 'python' 'which' )
optdepends=('gtk2: GTK+ look and feel'
            'libgl: emulator support')
options=('!strip')
source=("https://redirector.gvt1.com/edgedl/android/studio/ide-zips/$pkgver/android-studio-$pkgver-linux.tar.gz"
        "$pkgname.desktop")
sha256sums=('6e185b7f90653e01abb715a0fb58fc532d9b4bb36b3b1806035dc310b8260c96'
            '12ff377490cff386ba7b3ab9c94adfc2ca99515fdfad4773813268217dcd56b7')

package() {
  cd $srcdir/android-studio

  # Change the product name to produce a unique WM_CLASS attribute.
  mkdir -p idea
  unzip -p lib/resources.jar idea/AndroidStudioApplicationInfo.xml \
      | sed "s/\"Studio\"/\"Studio Canary\"/" >idea/AndroidStudioApplicationInfo.xml
  zip -r lib/resources.jar idea
  rm -r idea

  # Install the application.
  install -d $pkgdir/{opt/$pkgname,usr/bin}
  cp -a bin lib jbr license plugins product-info.json build.txt LICENSE.txt NOTICE.txt $pkgdir/opt/$pkgname
  ln -s /opt/$pkgname/bin/studio.sh $pkgdir/usr/bin/$pkgname

  # Add the icon and desktop file.
  install -Dm644 bin/studio.png $pkgdir/usr/share/pixmaps/$pkgname.png
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

  chmod -R ugo+rX $pkgdir/opt
}
