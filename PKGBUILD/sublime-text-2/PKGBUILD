pkgname=sublime-text2
pkgver=2.0.2
pkgrel=1
pkgdesc="Sophisticated text editor for code, html and prose (legacy version)"
arch=('x86_64')
url="https://www.sublimetext.com/2"
license=('custom: commercial')
depends=("libpng" "gtk2" "bash" "xdg-utils" "desktop-file-utils" "shared-mime-info")
conflicts=("sublime-text"
	"sublime-text-3"
	"sublime-text-4"
	"sublime-text-dev"
)
# EULA file: 'html2text --body-width=80 https://www.sublimetext.com/eula'
source=("https://download.sublimetext.com/Sublime%20Text%20${pkgver}%20x64.tar.bz2"
		"$pkgname-EULA"
        "$pkgname.desktop"
        "$pkgname.sh")
sha256sums=('01baed30d66432e30002a309ff0393967be1daba5cce653e43bba6bd6c38ab84'
            'dec26169ec941f1089810b7be599193577d0250f20e465d1399b062f71f2a244'
            '7a3d0ea23fc0eb8d1e4d322df96a18aab214f8f7ef5808a60f9ad4866c0fc14b'
            'f68914c1f346437dcb910d4492c47f562e387c2a28c654a82e96616ff664c35e')

package () {
  install -d "$pkgdir"/opt/sublime-text-2
  cp -r "Sublime Text 2"/* "$pkgdir"/opt/sublime-text-2/

  # launcher
  install -Dm0755 $pkgname.sh "$pkgdir"/usr/bin/subl

  # .desktop file and icons
  install -Dm0644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  for _res in 256x256 128x128 48x48 32x32 16x16; do
    install -d "$pkgdir"/usr/share/icons/hicolor/$_res/apps
    ln -s /opt/$pkgname/Icon/$_res/sublime_text.png \
      "$pkgdir"/usr/share/icons/hicolor/$_res/apps/$pkgname.png
  done

  # license
  install -Dm0644 $pkgname-EULA "$pkgdir"/usr/share/licenses/$pkgname/EULA
}