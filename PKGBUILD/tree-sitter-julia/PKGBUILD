pkgname=tree-sitter-julia
pkgver=0.23.0
pkgrel=1
pkgdesc='Julia grammar for tree-sitter'
arch=(x86_64)
url=https://github.com/tree-sitter/tree-sitter-julia/
license=(MIT)
groups=(tree-sitter-grammars)
makedepends=(
  git
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("${url}/archive/refs/tags/v${pkgver}.zip")
sha256sums=('198a8c70c4de204663020db93699dd2d7cc729e2d8f27dc5cac27c0ae90c99cb')

prepare()
{
  cd $pkgname-${pkgver}
  tree-sitter generate --no-bindings src/grammar.json
}

build()
{
  cd $pkgname-${pkgver}
  make PREFIX=/usr PARSER_URL=$url
}

package()
{
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname-${pkgver}
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
