pkgname=tree-sitter-ql-dbscheme
pkgver=0.23.0
pkgrel=1
pkgdesc='tree-sitter support for `.dbscheme` files (as used in CodeQL).'
arch=(x86_64)
url=https://github.com/tree-sitter/tree-sitter-ql-dbscheme/
license=(MIT)
groups=(tree-sitter-grammars)
makedepends=(
  git
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("${url}/archive/refs/tags/v${pkgver}.zip")
sha256sums=('99f5825154eace0b9588d3c4f4c3bf18684feb264bb2978ee52d8df629763a24')

prepare()
{
  cd $pkgname-${pkgver}
  tree-sitter generate --no-bindings src/grammar.json
}

build()
{
  cd $pkgname-${pkgver}
  make PREFIX=/usr PARSER_URL=$url
}

package()
{
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname-${pkgver}
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
